apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'jacoco'

buildscript {
    ext {
        kotlinVersion = '1.7.0'
    }
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

group 'com.github.seed'
version '1.0.1-SNAPSHOT'

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude:  [
                    'com/github/seed/RecordJson*.*',
                    'com/github/seed/RecordMap*.*'
            ])
        })
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude:  [
                    'com/github/seed/RecordJson*.*',
                    'com/github/seed/RecordMap*.*'
            ])
        })
    }
    violationRules {
        rule {
            limit {
                minimum = 0.95
            }
        }
    }
}

jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'com.opencsv:opencsv:5.7.0'
    implementation "org.thymeleaf:thymeleaf:3.0.11.RELEASE"
    implementation 'io.projectreactor:reactor-core:3.4.22'
    implementation 'org.mongodb:mongodb-driver-reactivestreams:4.1.0'
    implementation 'com.github.davidmoten:rxjava-jdbc:0.7.17'
    implementation 'com.github.everit-org.json-schema:org.everit.json.schema:1.11.1'

    testImplementation 'com.h2database:h2:1.4.200'
    testImplementation 'io.projectreactor:reactor-test:3.4.22'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation "io.kotlintest:kotlintest-runner-junit5:3.4.2"
    testImplementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    finalizedBy jacocoTestCoverageVerification
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}
